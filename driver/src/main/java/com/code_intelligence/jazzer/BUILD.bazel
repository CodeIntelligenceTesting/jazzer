load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@fmeum_rules_jni//jni:defs.bzl", "java_jni_library")
load("//:maven.bzl", "JAZZER_VERSION")

java_binary(
    name = "Jazzer",
    visibility = ["//visibility:public"],
    runtime_deps = [":jazzer_lib"],
)

java_jni_library(
    name = "jazzer_lib",
    srcs = ["Jazzer.java"],
    native_libs = select({
        "@platforms//os:windows": [],
        "//conditions:default": ["//driver/src/main/native/com/code_intelligence/jazzer:jazzer_preload"],
    }),
    visibility = ["//visibility:public"],
    deps = ["//driver/src/main/java/com/code_intelligence/jazzer/driver"],
)

write_file(
    name = "constants_java",
    out = "Constants.java",
    content = [
        "package com.code_intelligence.jazzer;",
        "public final class Constants {",
        "  public static final String JAZZER_VERSION = \"%s\";" % JAZZER_VERSION,
        "}",
    ],
)

java_library(
    name = "constants",
    srcs = [":constants_java"],
    visibility = ["//visibility:public"],
)
